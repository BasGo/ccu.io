!# datapoints.fn 1.2 CCU.IO
!#
!# Dieses Homematic-Script gibt eine Liste aller Datenpunkte als JSON String aus
!#
!# 3'2013-7'2013 hobbyquaker https://github.com/hobbyquaker
!#

string sDevId;
string sChnId;
string sDPId;
string sValue;
string sChnId;
string sDPId;
string oWId;

boolean dpFirst = true;

Write('{');

foreach (sDevId, root.Devices().EnumUsedIDs()) {

    object oDevice   = dom.GetObject(sDevId);
    boolean bDevReady = oDevice.ReadyConfig();
    
    if (bDevReady) {


        foreach(sChnId, oDevice.Channels()) {
            object oChannel = dom.GetObject(sChnId);

            integer iChnDir     = oChannel.ChnDirection();

            if (iChnDir == 2) {
                object oWork = oChannel.DPByHssDP('WORKING');
                if (oWork) {
                    if (dpFirst) {
                      dpFirst = false;
                    } else {
                      WriteLine(',');
                    }
                    oWId = oWork.ID();
                    Write('"' # oWId # '":{');
                    Write('"Name":"' # oWork.Name() # '","TypeName":"HSSDP","Operations":"5",');
                    Write('"ValueType":2,"ValueUnit":"","Timestamp":"' # oWork.Timestamp());
                    Write('","Value":null,"Parent":' # sChnId # '}');
                }

                object oWork = oChannel.DPByHssDP('DIRECTION');
                if (oWork) {
                    if (dpFirst) {
                      dpFirst = false;
                    } else {
                      WriteLine(',');
                    }
                    oWId = oWork.ID();
                    Write('"' # oWId # '":{');
                    Write('"Name":"' # oWork.Name() # '","TypeName":"HSSDP","Operations":"5",');
                    Write('"ValueType":16,"ValueList":"' # oWork.ValueList() # '","Timestamp":"' # oWork.Timestamp());
                    Write('","Value":null,"Parent":' # sChnId # '}');
                }

            }



            foreach(sDPId, oChannel.DPs().EnumUsedIDs()) {
                object oDP = dom.GetObject(sDPId);
                if(oDP) {

                    if (dpFirst) {
                      dpFirst = false;
                    } else {
                      WriteLine(',');
                    }

                    string sValueType = oDP.ValueType();
                    Write('"' # sDPId # '":{');
                    Write('"Name":"');
                    WriteURL(oDP.Name());
                    Write('","TypeName":"' # oDP.TypeName());
                    Write('","Operations":' # oDP.Operations());
                    Write(',"ValueType":' # sValueType);
                    Write(',"ValueUnit":"' # oDP.ValueUnit());
                    Write('","Timestamp":"' # oDP.Timestamp());
                    Write('","Value":');

                    if (sValueType == 20) {
                        Write('"');
                        WriteURL(oDP.Value());
                        Write('"');
                    } else {
                        sValue = oDP.Value();
                        if (sValueType == 2) {
                            if (sValue) {
                                Write("true");
                            } else {
                                Write("false");
                            }
                        } else {
                           if (sValue == "") {
                                Write("0");
                           } else {
                                Write(sValue);
                           }
                        }
                    }

                    Write(',"Parent":' # sChnId);
                    Write('}');
                }
            }


        }

    }
}
Write('}');
