!# variables.fn 1.3 CCU.IO
!#
!# Dieses Script gibt die Systemvariablen als JSON String aus
!#
!# 3'2013-7'2013 hobbyquaker https://github.com/hobbyquaker
!#

object oSysVar;
string sSysVarId;
string sValueType;
string sValue;
boolean bFirst = true;

Write('{');

WriteLine('"40":{"name":"Alarmmeldungen","TypeName":"VARDP","dpInfo":"Anzahl%20Alarmmeldungen","valueMin":0,"valueMax":65000,"valueUnit":"","valueType":16,"valueSubType":0,"valueList":""},');
Write('"41":{"name":"Servicemeldungen","TypeName":"VARDP","dpInfo":"Anzahl%20Servicemeldungen","valueMin":0,"valueMax":65000,"valueUnit":"","valueType":16,"valueSubType":0,"valueList":""}');

foreach (sSysVarId, dom.GetObject(ID_SYSTEM_VARIABLES).EnumUsedIDs()) {

  WriteLine(',');

  
  oSysVar     = dom.GetObject(sSysVarId); 
  sValueType  = oSysVar.ValueType();
    
  Write('"' # sSysVarId # '":{"Name":"');
  WriteURL(oSysVar.Name());
  Write('","TypeName":"' # oSysVar.TypeName());
  Write('","DPInfo":"');
  WriteURL(oSysVar.DPInfo());

  Write('","Value":');
  if (sValueType == 20) {
    Write('"');
  	WriteURL(oSysVar.Value());
    Write('"');
  } else {
    sValue = oSysVar.Value();
    if (sValueType == 2) {
        if (sValue) {
            Write("true");
        } else {
            Write("false");
        }
    } else {
       if (sValue == "") {
            Write("0");
       } else {
            Write(sValue);
       }
    }
  }
  string sValueMin = oSysVar.ValueMin();

  if (sValueMin == '') {
      sValueMin = 'null';
  }

  string sValueMax = oSysVar.ValueMax();

  if (sValueMax == '') {
      sValueMax = 'null';
  }

  Write(',"Timestamp":"' # oSysVar.Timestamp());
  Write('","ValueMin":' # sValueMin # ',"ValueMax":' # sValueMax # ',"ValueUnit":"');
  WriteURL(oSysVar.ValueUnit());
  Write('","ValueType":' # sValueType # ',"ValueSubType":' # oSysVar.ValueSubType());
  
  if (sValueType == 2) {
    Write(',"ValueList":"');
    WriteURL(oSysVar.ValueName0() # ';' # oSysVar.ValueName1());
  } else {
    Write(',"ValueList":"');
    WriteURL(oSysVar.ValueList());
  }

  Write('"}');
  
}

Write('}');