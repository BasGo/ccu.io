!# devices.fn 1.0
!#
!# Dieses Homematic-Script gibt eine Liste aller Geräte als JSON String aus
!#
!# 3'2013-7'2013 hobbyquaker https://github.com/hobbyquaker
!#

string sDevId;
string sChnId;
string sDPId;

Write('{');

    boolean dFirst = true;

    foreach (sDevId, root.Devices().EnumUsedIDs()) {
    if (dFirst) {
      dFirst = false;
    } else {
      WriteLine(',');
    }

    object oDevice   = dom.GetObject(sDevId);
    boolean bDevReady = oDevice.ReadyConfig();
    string sDevInterfaceId = oDevice.Interface();
    string sDevInterface   = dom.GetObject(sDevInterfaceId).Name();

    if (bDevReady) {

        Write('"' # sDevId # '":{"Name":"');
        WriteURL(oDevice.Name());
        Write('","Address":"' # oDevice.Address() # '","Interface":"' # sDevInterface # '","HssType":"' # oDevice.HssType());
        Write('","Channels":[');

        string sChnId;

        boolean cFirst = true;
        foreach(sChnId, oDevice.Channels()) {
            if (cFirst) {
                cFirst = false;
            } else {
                Write(',');
            }
            Write(sChnId);
        }
        Write(']}');
    }
}
Write('}');
